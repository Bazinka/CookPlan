buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

apply plugin: 'io.fabric'

apply from: "$rootDir/constants.gradle"

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion project.ext.targetSdk
    defaultConfig {
        applicationId "com.cookplan"
        minSdkVersion project.ext.minSdk
        targetSdkVersion project.ext.targetSdk
        versionCode project.ext.versionCode
        versionName project.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release
        debug
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
            multiDexEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            signingConfig signingConfigs.debug
            multiDexEnabled true
        }
    }
}
def Properties props = new Properties()
def propFile = new File('signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('DEBUG_keystoreDir') && props.containsKey('DEBUG_keystorePassword') &&
            props.containsKey('DEBUG_keyAlias') && props.containsKey('DEBUG_aliasPassword')) {
        android.signingConfigs.debug.storeFile = file(props['DEBUG_keystoreDir'])
        android.signingConfigs.debug.storePassword = props['DEBUG_keystorePassword']
        android.signingConfigs.debug.keyAlias = props['DEBUG_keyAlias']
        android.signingConfigs.debug.keyPassword = props['DEBUG_aliasPassword']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha03'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha03'
    implementation 'com.google.android.material:material:1.1.0-alpha04'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-places:16.0.0'

    implementation "com.google.firebase:firebase-auth:16.2.0"
    implementation "com.google.firebase:firebase-core:16.0.8"
    implementation "com.google.firebase:firebase-database:16.1.0"
    implementation "com.google.firebase:firebase-storage:16.1.0"


    implementation('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }

    implementation 'com.rmtheis:tess-two:5.4.1'
    implementation 'net.danlew:android.joda:2.9.4.1'
//    implementation 'com.bettervectordrawable:lib:0.8'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.0.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'org.jsoup:jsoup:1.10.2'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
}


apply plugin: 'com.google.gms.google-services'